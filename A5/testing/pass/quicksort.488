%
% Quicksort, using inner functions instead of passing variables.
%
{
  var n, a[0..100], i : integer
  proc swap(i:integer,j:integer){
    var t : integer
    t := a[i]
    a[i] := a[j]
    a[j] := t
  }
  proc qsort(st:integer,ed:integer) {
    var piv : integer
    proc part(){
      var j : integer
      piv := st
      swap(piv,ed)
      j := st
      while j < ed do 
        if a[j] < a[ed] then
          swap(piv, j)
          piv := piv + 1
        fi
        j := j + 1
      end
      swap(piv,ed)
    }
    if st < ed then
      part()
      qsort(st,piv-1)
      qsort(piv+1,ed)
    fi
  }
  get n
  %@input=4
  i := 0
  while i < n do
    get a[i]
    i := i + 1
  end
  %@input=4
  %@input=2
  %@input=3
  %@input=1
  qsort(0,n-1)
  i := 0
  while i < n do
    put a[i],newline
    i := i + 1
  end
  %@output=1
  %@output=2
  %@output=3
  %@output=4
}
